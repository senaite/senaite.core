*** Settings ***

Documentation    Generally useful keywords for Bika tests.
...              To include this file, use this:
...              *** settings ***
...              Resource  keywords.txt


*** Keywords ***

Log in
    [Arguments]  ${userid}  ${password}

    Go to                          http://localhost:55001/plone/login_form

    # cheeky but I want, so sneaked in here
    Maximize Browser Window

    # This is obvious, don't waste time testing the Plone's login form
    #Page should contain element     __ac_name
    #Page should contain element     __ac_password
    #Page should contain button      Log in

    Input text                      __ac_name      ${userid}
    Input text                      __ac_password  ${password}
    Click Button  Log in


Log out
    Go to                          http://localhost:55001/plone/logout
    Wait until page contains       You are now logged out


Select from dropdown
    [Arguments]        ${locator}
    ...                ${search_text}=
    ...                ${item_number}=1
    sleep    1
    Input text         ${locator}  ${search_text}
    sleep    1
    Click Element      xpath=//div[contains(@class,'cg-colItem')][${item_number}]
    sleep    0.5


SelectDate
    [Arguments]  ${Element}=
    ...          ${Date}=
    ${Date} =            Convert To Integer    ${Date}
    ${Date} =            Convert To String     ${Date}
    Click Element  ${Element}
    Wait Until Page Contains Element  ui-datepicker-div
    sleep                1
    Click Link  ${Date}


SelectPrevMonthDate
    [Arguments]  ${Element}=
    ...          ${Date}=
    Click Element        ${Element}
    Wait Until Page Contains Element  ui-datepicker-div
    sleep                1
    Click Element        xpath=//div[@id='ui-datepicker-div']//a[@title='Prev']
    ${Date} =            Convert To Integer    ${Date}
    ${Date} =            Convert To String     ${Date}
    sleep                1
    Click Link           ${Date}

SelectNextMonthDate
    [Arguments]  ${Element}=
    ...          ${Date}=
    Click Element        ${Element}
    Wait Until Page Contains Element  ui-datepicker-div
    sleep                1
    Click Element        xpath=//div[@id='ui-datepicker-div']//a[@title='Next']
    ${Date} =            Convert To Integer    ${Date}
    ${Date} =            Convert To String     ${Date}
    sleep                1
    Click Link           ${Date}

Execute transition ${transition} on items in form_id ${form_id}
    Select checkbox     xpath=//input[@id='${form_id}_select_all']
    Set Selenium Timeout        30
    Click Element       ${transition}_transition
    Set Selenium Timeout        10

Execute transition ${transition} inside ClientARView/ManageResults
    Click Element  css=a[title="Change the state of this item"]
    Click Element  workflow-transition-${transition}

Shleep
    [Arguments]  ${amount}=
    ...          ${comment}=
    Log  Sleeping ${amount}: ${comment}  WARN
    sleep  ${amount}

Wait For Ajax
    Wait for Condition   return window.jQuery.active == 0;
